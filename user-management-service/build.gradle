plugins {
    id "application"
    id "org.springframework.boot"
    id "org.springframework.cloud.contract"
}
application{
    mainClassName("com.mflyyou.contract.UserManagementServiceApplication")
}
dependencies {
    implementation(project(":contract-sdk"))
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation("io.rest-assured:spring-mock-mvc")
    testImplementation('org.springframework.cloud:spring-cloud-contract-verifier')
    testImplementation("org.springframework.cloud:spring-cloud-starter-contract-stub-runner")
}
contracts {
    testFramework = "JUNIT5"
    failOnNoContracts = false
    sourceSet = "contractTest"
    baseClassForTests = "com.mflyyou.contract.BaseContractTest"
    contractsDslDir = new File("${projectDir}/src/contractTest/resources")
    baseClassMappings {
        baseClassMapping(".*usermgmt.user.*", "com.mflyyou.contract.user.BaseContractUserTest")
    }
}

publishing {
    repositories {
        maven {
            allowInsecureProtocol=true
            url = project.version.endsWith("SNAPSHOT") ? "$mavenSnapshotsUrl" : "$mavenReleaseUrl"
            credentials {
                username = "$mavenUsername"
                password = "$mavenPassword"
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifact bootJar
            artifact verifierStubsJar
        }
    }
}